buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        flatDir {
            dirs 'libs'
        }
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
    }
}

apply plugin: 'com.android.library'

android {
compileSdkVersion 28
buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
    buildTypes{
        debug {

        }
        release {

        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

repositories {
    mavenCentral()
}

def getGitHubRelease = { name, githubRepo = "snapcall/snapcall_sdk_android" ->
    Collection v = name.split(":")
    if (v.size() != 2)
        throw new GradleException("getGithubRelease: wrong aar name [name:release]")
    String aarVersion = v.get(1)
    String aarName = v.get(0)
    String url = "https://github.com/${githubRepo}/releases/download/"
    File file = new File("$projectDir/libs/${name}.aar")
    if (!file.exists()) {
        delete fileTree("$projectDir/libs/").matching { include "${aarName}*.aar" }
        file.parentFile.mkdirs()
        try {
            new URL("${url}${aarVersion}/${aarName}.aar").withInputStream { downloadStream ->
                file.withOutputStream { fileOut ->
                    fileOut << downloadStream
                }
            }
        } catch (Exception e) {
            throw new GradleException("getGithubRelease: could not download aar file")
        }
    }
    if (!file.exists())
        throw new GradleException("getGithubRelease: could not find aar file")
    files(file.absolutePath)
}

dependencies {

    implementation getGitHubRelease('snapcall_android_framework:2.3.8')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'org.webrtc:google-webrtc:1.0.27771'
    implementation "com.facebook.react:react-native:+"
}
